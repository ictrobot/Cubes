apply plugin: "java"

sourceCompatibility = 1.7
sourceSets.main.java.srcDirs = ["src/"]

project.ext.mainClassName = "ethanjones.cubes.core.platform.desktop.LwjglLauncher"
project.ext.assetsDir = new File("../assets");

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
}

task dist(dependsOn: classes, type: Jar) {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from { configurations.compile.collect { zipTree(it) } }
    from files(project.assetsDir);

    archiveName = 'desktop-lwjgl.jar'

    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

eclipse {
    project {
        name = appName + "-desktop-lwjgl"
        linkedResource name: 'assets', type: '2', location: 'PARENT-1-PROJECT_LOC/android/assets'
    }
}

task afterEclipseImport(description: "Post processing after project generation", group: "IDE") {
    doLast {
        def classpath = new XmlParser().parse(file(".classpath"))
        new Node(classpath, "classpathentry", [kind: 'src', path: 'assets']);
        def writer = new FileWriter(file(".classpath"))
        def printer = new XmlNodePrinter(new PrintWriter(writer))
        printer.setPreserveWhitespace(true)
        printer.print(classpath)
    }
}

publishing {
    publications {
        jar(MavenPublication) {
            artifact dist
            groupId 'ethanjones.cubes'
            artifactId 'desktop-lwjgl'
            version getMavenVersionString()
        }
    }
    repositories {
        maven {
            url getMavenRepo()
        }
    }
}

publish.dependsOn(['dist'])