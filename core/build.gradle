apply plugin: "java"
sourceCompatibility = 1.7
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceSets.main.java.srcDirs = ["src/"]
eclipse.project {
    name = appName + "-core"
}
dependencies {
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    archiveName = "core-sources.jar"
}

jar.archiveName = "core.jar"

task properites() {
    Properties props = new Properties()
    project.getBuildDir().mkdir();
    File propsFile = new File(project.getBuildDir(), '/core.properties')
    propsFile.delete();
    props.setProperty('gdxVersion', gdxVersion.toString())
    props.setProperty('androidBuildToolsVersion', androidBuildToolsVersion.toString())
    props.setProperty('androidSdkVersion', androidSdkVersion.toString())
    props.setProperty('dataVersion', dataVersion.toString())
    props.setProperty('buildMajor', buildProperties.major.toString())
    props.setProperty('buildMinor', buildProperties.minor.toString())
    props.setProperty('buildPoint', buildProperties.point.toString())
    props.setProperty('buildVersion', buildProperties.version.toString())
    props.setProperty('buildNumber', buildProperties.build.toString())
    props.store(propsFile.newWriter(), null)
}

publishing {
    publications {
        jar(MavenPublication) {
            artifact tasks.jar
            artifact tasks.sourcesJar {
                classifier = 'sources'
            }
            artifact(new File(project.getBuildDir(), '/core.properties')) {
                classifier "properties"
            }
            groupId 'ethanjones.cubes'
            artifactId 'core'
            version getMavenVersionString()

            pom.withXml {
                def repositories = asNode().appendNode('repositories')
                def libgdxSnapshots = repositories.appendNode('repository')
                libgdxSnapshots.appendNode('id', 'libgdxSnapshots')
                libgdxSnapshots.appendNode('url', 'https://oss.sonatype.org/content/repositories/snapshots/')
                def libgdxReleases = repositories.appendNode('repository')
                libgdxReleases.appendNode('id', 'libgdxReleases')
                libgdxReleases.appendNode('url', 'https://oss.sonatype.org/content/repositories/releases/')
                def dataRepo = repositories.appendNode('repository')
                dataRepo.appendNode('id', 'dataRepo')
                dataRepo.appendNode('url', 'https://raw.githubusercontent.com/ictrobot/mvn-repo/master/')

                def dependencies = asNode().appendNode('dependencies')
                def dependencyLibgdx = dependencies.appendNode('dependency')
                dependencyLibgdx.appendNode('groupId', 'com.badlogicgames.gdx')
                dependencyLibgdx.appendNode('artifactId', 'gdx')
                dependencyLibgdx.appendNode('version', gdxVersion)
                def dependencyData = dependencies.appendNode('dependency')
                dependencyData.appendNode('groupId', 'ethanjones')
                dependencyData.appendNode('artifactId', 'data')
                dependencyData.appendNode('version', dataVersion)
            }
        }
    }
    repositories {
        maven {
            url getMavenRepo()
        }
    }
}

publish.dependsOn(['properites'])
