buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13+'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    }
}

ext {
    buildProperties = new Properties()
    file("assets/version").withReader { reader ->
        buildProperties.load(reader)
        buildProperties.version = buildProperties.major + "." + buildProperties.minor + "." + buildProperties.point
        println "VERSION: " + buildProperties.version
    }

    build = null;
    if (hasGit()) {
        build = shell("git rev-list HEAD --count").text.trim()
        hash = shell("git rev-parse HEAD").text.trim()
        file("assets/build").write("build=" + build + System.getProperty("line.separator") + "hash=" + hash, 'UTF-8')
        println "BUILD:   " + build
        println "HASH:    " + hash
    } else {
        println "Git was not found in path!"
        file("assets/build").delete()
    }

    localProperties = new Properties()
    if (file("build.properties").exists()) {
        file("build.properties").withReader { reader ->
            localProperties.load(reader)
        }
    }
    if (localProperties.CUBES_KEYSTORE_FILE == null) localProperties.CUBES_KEYSTORE_FILE = "keystore"
    if (localProperties.CUBES_KEYSTORE_PASSWORD == null) localProperties.CUBES_KEYSTORE_PASSWORD = "password"
    if (localProperties.CUBES_KEY_ALIAS == null) localProperties.CUBES_KEY_ALIAS = "android"
    if (localProperties.CUBES_KEY_PASSWORD == null) localProperties.CUBES_KEY_PASSWORD = "password"
    if (localProperties.CUBES_MAVEN_REPO == null) localProperties.CUBES_MAVEN_REPO = getRootProject().getProjectDir().absolutePath + "/repo/"
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "maven-publish"

    version = System.getenv("BUILD_NUMBER")

    ext {
        appName = 'Cubes'
        gdxVersion = '1.5.0'
        androidBuildToolsVersion = '21.1.0'
        androidSdkVersion = 21
        dataVersion = '1.0'
        buildProperties = getRootProject().buildProperties
        build = getRootProject().build
        localProperties = getRootProject().localProperties
        isRelease = false
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://raw.githubusercontent.com/ictrobot/mvn-repo/master/snapshots" }
        maven { url "https://raw.githubusercontent.com/ictrobot/mvn-repo/master/releases" }
    }
}

project(":core") {
    apply plugin: "java"

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "ethanjones:data:$dataVersion"
    }
}

project(":desktop") {
    apply plugin: "java"

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":client") {
    apply plugin: "java"

    dependencies {
        compile project(":desktop")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    }
}

project(":server") {
    apply plugin: "java"

    dependencies {
        compile project(":desktop")
        compile "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    }
}

project(":android") {
    apply plugin: "android-sdk-manager"
    apply plugin: "android"

    configurations { natives }

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
    }
}
tasks.eclipse.doLast {
    delete ".project"
}

task ci(dependsOn: ['core:publish', 'server:dist', 'client:dist', 'android:assembleDebug', 'android:assembleRelease']) << {

}

task travis(dependsOn: ['core:jar', 'server:dist', 'client:dist', 'android:assembleDebug']) {

}

def String getMavenVersionString() {
    def str = buildProperties.version;
    if (!isRelease) {
        str = str + "-SNAPSHOT"
    }
    return str
}

def String getMavenRepo() {
    def str = localProperties.CUBES_MAVEN_REPO + "/"
    if (isRelease) {
        str = str + "releases"
    } else {
        str = str + "snapshots"
    }
    return str
}

def boolean hasGit() {
    def proc = "git rev-parse --short HEAD".execute()
    proc.waitFor()
    return proc.exitValue() == 0;
}

def Process shell(String str) {
    def proc = str.execute()
    proc.waitFor()
    return proc
}