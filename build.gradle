buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url 'https://github.com/steffenschaefer/gwt-gradle-plugin/raw/maven-repo/' }
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.4'
        classpath 'com.android.tools.build:gradle:0.12.2'
        classpath 'com.github.jtakakura:gradle-robovm-plugin:0.0.9'
    }
}

def build = new Properties()
file("android/assets/version").withReader { reader ->
    build.load(reader)
    build.version = build.major + "." + build.minor + "." + build.point
    println "VERSION: " + build.version
    println "BUILD:   " + build.build
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "maven-publish"

    //version = System.getenv("BUILD_NUMBER")

    ext {
        appName = 'Modular World'
        gdxVersion = '1.3.2-SNAPSHOT'
        roboVMVersion = '0.0.14'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":core") {
    apply plugin: "java"

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    }
}

project(":desktop-common") {
    apply plugin: "java"

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":desktop-lwjgl") {
    apply plugin: "java"

    dependencies {
        compile project(":desktop-common")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    }
}

project(":desktop-headless") {
    apply plugin: "java"

    dependencies {
        compile project(":desktop-common")
        compile "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    }
}

project(":android") {
    apply plugin: "android"

    configurations { natives }

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
    }
}

project(":ios") {
    apply plugin: "java"
    apply plugin: "robovm"

    configurations { natives }

    dependencies {
        compile project(":core")
        compile "org.robovm:robovm-rt:${roboVMVersion}"
        compile "org.robovm:robovm-cocoatouch:${roboVMVersion}"
        compile "com.badlogicgames.gdx:gdx-backend-robovm:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
    }
}

tasks.eclipse.doLast {
    delete ".project"
}

task ci(dependsOn: ['publish']) << {

}