buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
    }
}

ext {
    buildProperties = new Properties()
    file("android/assets/version").withReader { reader ->
        buildProperties.load(reader)
        buildProperties.version = buildProperties.major + "." + buildProperties.minor + "." + buildProperties.point
        println "VERSION: " + buildProperties.version
        println "BUILD:   " + buildProperties.build
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "maven-publish"

    version = System.getenv("BUILD_NUMBER")

    ext {
        appName = 'Modular World'
        gdxVersion = '1.3.2-SNAPSHOT'
        androidBuildToolsVersion = '19.1.0'
        androidSdkVersion = 19
        dataVersion = 1.0
        buildProperties = getRootProject().buildProperties
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://raw.githubusercontent.com/ictrobot/mvn-repo/master/" }
    }
}

project(":core") {
    apply plugin: "java"

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "ethanjones.data:java:$dataVersion"
    }
}

project(":desktop-common") {
    apply plugin: "java"

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":desktop-lwjgl") {
    apply plugin: "java"

    dependencies {
        compile project(":desktop-common")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    }
}

project(":desktop-headless") {
    apply plugin: "java"

    dependencies {
        compile project(":desktop-common")
        compile "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
    }
}

project(":android") {
    apply plugin: "android"

    configurations { natives }

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
    }
}
tasks.eclipse.doLast {
    delete ".project"
}

task ci(dependsOn: ['core:publish', 'desktop-headless:publish', 'desktop-lwjgl:publish', 'android:assembleDebug', 'android:assembleRelease']) << {

}